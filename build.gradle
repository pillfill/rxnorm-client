apply plugin: 'jsonschema2pojo'
apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.jsonschema2pojo:jsonschema2pojo-gradle-plugin:0.4.13'
    }
}

repositories {
    mavenCentral()
}

group = 'com.apothesource.pillfill'
archivesBaseName = 'rxnorm-client'
version = '1.0.0-SNAPSHOT'
def isReleaseVersion = !version.endsWith("SNAPSHOT")


dependencies {
    compile 'com.google.code.gson:gson:2.3.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-junit:2.0.0.0'
}

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java", "src/gen/java"]
        }
    }
    test{
        java{
            srcDirs = ["src/main/java", "src/gen/java", "src/test/java"]
        }
    }
}

test {
    exclude '*/service/*'
}

task integrationTest(type: Test){
    include '*/service/*'
}

// Each configuration is set to the default value
jsonSchema2Pojo {
    source = files("${sourceSets.main.output.resourcesDir}/schemas")
    targetDirectory = file("src/gen/java")
    sourceType = 'jsonSchema'
    annotationStyle = 'gson'
    removeOldOutput = true
    includeToString = false
    includeHashcodeAndEquals = false
}

javadoc{
    failOnError false
    exclude "**com/apothesource/pillfill/datamodel/**"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
artifacts {
    archives javadocJar, sourcesJar
}
signing {
    required=isReleaseVersion
    if(isReleaseVersion) sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'PillFill RxNorm Client'
                packaging 'jar'
                description 'A lightweight java client library for the NIH RxNav (RxNorm) services'
                url 'https://github.com/pillfill/rxnorm-client'

                scm {
                    connection 'scm:git:git://github.com/pillfill/rxnorm-client.git'
                    developerConnection 'scm:git:git://github.com/pillfill/rxnorm-client.git'
                    url 'https://github.com/pillfill/rxnorm-client'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://raw.githubusercontent.com/pillfill/rxnorm-client/master/license.txt'
                    }
                }

                developers {
                    developer {
                        id 'rammic'
                        name 'Michael Ramirez'
                        email 'michael@pillfill.com'
                    }
                }
            }
        }
    }
}